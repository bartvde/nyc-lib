plugins {
    id 'base'
    id 'jetty'
    id 'org.hidetake.ssh' version '1.1.2'
	id 'com.eriwen.gradle.css' version '1.11.1'
}

repositories {
  mavenCentral() //or jcenter()
}
configurations {
  closureCompiler
}
dependencies {
  closureCompiler 'com.google.javascript:closure-compiler:v20150609'
}

ext {
	ver = 'v0.0.1-beta.2'
	remoteDir = "nyc-lib/${ver}"
	jsSrcDir = "${projectDir}/src/main/js/nyc"
	cssSrcDir = "${projectDir}/src/main/css"
	olJsFiles = [
		'nyc.js', 
		'collapsible.js', 
		'collapsible-radio.js', 
		'collapsible-month-range.js', 
		'content.js', 
		'directions.js',
		'geocoder.js', 
		'lang.js', 
		'legend.js', 
		'locate.js', 
		'share.js',
		'zoomsearch.js',
		'ol/ol.js', 
		'ol/draw.js', 
		'ol/featuretip.js', 
		'ol/locate.js', 
		'ol/popup.js', 
		'ol/control/zoomsearch.js',
		'ol/geoserver/getfeature.js', 
		'ol/source/arcgiscache.js', 
		'ol/source/decorating.js', 
		'ol/source/filteringandsorting.js',
		'ol/layer/baselayer.js'
	]
	leafJsFiles = [
		'nyc.js', 
		'collapsible.js', 
		'collapsible-radio.js', 
		'collapsible-month-range.js', 
		'content.js', 
		'directions.js',
		'geocoder.js', 
		'lang.js', 
		'legend.js', 
		'locate.js', 
		'share.js',
		'zoomsearch.js',
		'leaf/leaf.js', 
		'leaf/locate.js', 
		'leaf/zoomsearch.js'
	]
	cartoJsFiles = [
		'nyc.js', 
		'collapsible.js', 
		'collapsible-radio.js', 
		'collapsible-month-range.js', 
		'content.js', 
		'directions.js',
		'geocoder.js', 
		'lang.js', 
		'legend.js', 
		'locate.js', 
		'share.js',
		'zoomsearch.js',
		'leaf/leaf.js', 
		'leaf/locate.js', 
		'leaf/zoomsearch.js',
		'carto/view.js',
		'carto/chart.js'
	]
	olCssFiles = [
		'control-common.css', 
		'collapsible.css', 
		'directions.css', 
		'draw.css', 
		'featuretip.css', 
		'lang.css', 
		'legend.css', 
		'popup.css', 
		'share.css', 
		'zoomsearch.css'
	]
	leafCssFiles = [
		'control-common.css', 
		'collapsible.css', 
		'directions.css', 
		'lang.css', 
		'legend.css', 
		'share.css', 
		'zoomsearch.css'
	]
	cartoCssFiles = [
		'control-common.css', 
		'chart.css', 
		'collapsible.css', 
		'directions.css', 
		'lang.css', 
		'legend.css', 
		'share.css', 
		'zoomsearch.css'
	]
	closureArgs = [
		'--charset=utf8',
		'--warning_level=VERBOSE',
		'--compilation_level=WHITESPACE_ONLY'
	]
	currentLib = 'leaf'
}

task jsdoc(type: JsDoc) {
	sourceDir = "${jsSrcDir}/nyc"
	destinationDir = "${buildDir}/${ver}/doc"
}

task minifyJsOl(type: MinifyJs) {
	version = ver
	libName = 'nyc-ol-lib'
	fileNames = olJsFiles
	sourceDir = jsSrcDir
	destinationDir = "${buildDir}/${ver}/js"
}

task minifyJsLeaf(type: MinifyJs) {
	version = ver
	libName = 'nyc-leaf-lib'
	fileNames = leafJsFiles
	sourceDir = jsSrcDir
	destinationDir = "${buildDir}/${ver}/js"
}

task minifyJsCarto(type: MinifyJs) {
	version = ver
	libName = 'nyc-carto-lib'
	fileNames = leafJsFiles
	sourceDir = jsSrcDir
	destinationDir = "${buildDir}/${ver}/js"
}

task minifyCssOl(type: MinifyCss) {
	libName = 'nyc-ol-lib'
	fileNames = olCssFiles
	sourceDir = cssSrcDir
	destinationDir = "${buildDir}/${ver}/css"
}

task minifyCssLeaf(type: MinifyCss) {
	libName = 'nyc-leaf-lib'
	fileNames = leafCssFiles
	sourceDir = cssSrcDir
	destinationDir = "${buildDir}/${ver}/css"
}

task minifyCssCarto(type: MinifyCss) {
	libName = 'nyc-carto-lib'
	fileNames = cartoCssFiles
	sourceDir = cssSrcDir
	destinationDir = "${buildDir}/${ver}/css"
}

task allJs(dependsOn: [minifyJsOl, minifyJsLeaf, minifyJsCarto]) << {}
task allCss(dependsOn: [minifyCssOl, minifyCssLeaf, minifyCssCarto]) << {}

task prepareFiles(dependsOn: [allJs, allCss]) << {}
prepareFiles.doFirst {
	copy {
			from 'src/main'
			include '**'
			into "${buildDir}/${ver}/src"
		}
}

task archive(type: Zip, dependsOn: [jsdoc, prepareFiles]) {
	archiveName = "nyc-lib-${ver}.zip"
	from {"${buildDir}/${ver}"}
}
archive.doFirst {
	copy {
		from 'build'
		include '*.debug.js'
		into "${buildDir}/${ver}"
	}
}


remotes {
	deployTarget {}
}

task deploy(dependsOn: []) << {
	def archiveDir = project.ext['archive.dir']
	def mobileDir = project.ext['mobile.dir']
	def deployDir = "${mobileDir}/${remoteDir}"
	
	remotes.deployTarget.host = project.ext["${env}.host"]
    remotes.deployTarget.user = project.ext["${env}.user"]	
    remotes.deployTarget.identity = file("${System.properties['user.home']}/.ssh/id_rsa")
	
	ssh.run {
        session(remotes.deployTarget) {
        	execute "mkdir ${archiveDir}"
            put "build/distributions/${archive.archiveName}", archiveDir
            execute "cp -R ${deployDir} ${deployDir}.bak"
            execute "rm -rf ${deployDir}"
            execute "unzip ${archiveDir}/${archive.archiveName} -d ${deployDir}"
            execute "rm -rf ${deployDir}.bak"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion '2.9'
}

class JsDoc extends DefaultTask {
	def jsdocDir = '../jsdoc'
	def sourceDir = ''
	def destinationDir = ''
	@TaskAction
	public void build(){
		def docs = new File(destinationDir)
		def scriptName = "${destinationDir}/doc.sh"
		docs.mkdirs()
		def script = new File(scriptName)
		script.text = "cd ${jsdocDir}\n" +
			"./jsdoc --verbose --recurse ${sourceDir} --destination ${destinationDir}"
		ant.chmod(file: scriptName, perm: '0755')
		scriptName.execute()
	}
}

class MinifyCss extends DefaultTask {
	def libName = ''
	def fileNames = []
	def sourceDir = ''
	def destinationDir = ''
	@TaskAction
	public void minify(){
		def dir = new File(destinationDir)
		dir.mkdirs()
		def cssOut = new File("${destinationDir}/${libName}.css")
		fileNames.each{ file ->
			def css = new File("${sourceDir}/${file}").getText('UTF-8')
			css = css.replaceAll(/[\n\r]+\s*/, '')
			css = css.replaceAll(/\s+/, ' ')
			css = css.replaceAll(/\s?([:,;{}])\s?/, '$1')
			css = css.replaceAll(/([\s:]0)(px|pt|%|em)/, '$1')
			css = css.replaceAll(/\/\*[\d\D]*?\*\//, '')
			cssOut.append(css)
		}		
	}
}

class MinifyJs extends JavaExec {
	def libName = ''
	def version = ''
	def fileNames = []
	def sourceDir = ''
	def destinationDir = ''
	def compilationLevel = 'WHITESPACE_ONLY'
	def warningLevel = 'VERBOSE'
	def sourceMapLocationMapping = '' 
	def compilerOptions = []
	public MinifyJs() {
		main 'com.google.javascript.jscomp.CommandLineRunner'
		classpath project.configurations.closureCompiler
	}
	@TaskAction
	@Override
	public void exec(){
		def arguments = []
		for (def i = 0; i < fileNames.size(); i++) { //todo can we use diff loop syntax
		  	arguments << "${sourceDir}/${fileNames[i]}"
		}
		for (def i = 0; i < compilerOptions.size(); i++) {
		  	arguments << compilerOptions[i]
		}
		arguments << '--charset=utf8'
		arguments << "--warning_level=${warningLevel}"
		arguments << "--compilation_level=${compilationLevel}"
		arguments << "--create_source_map=${libName}.sourcemap.json"
		arguments << "--js_output_file=${libName}.js"
		arguments << "--source_map_location_mapping=\"${this.getSourceMapLocationMapping()}\""
		arguments << "--output_wrapper=\"/* ${libName}-${version} */ %output% //# sourceMappingURL=${libName}.sourcemap.json\""
		args arguments
		super.exec();
		this.move()
	}
	private String getSourceMapLocationMapping(){
		if (sourceMapLocationMapping != '') return sourceMapLocationMapping;
		return "${sourceDir}|../src/js"
	}
	private void move(){
		def dir = new File(destinationDir)
		dir.mkdirs()
		ant.move(file: "${libName}.js", todir: destinationDir)
		ant.move(file: "${libName}.sourcemap.json", todir: destinationDir)	
	}
}